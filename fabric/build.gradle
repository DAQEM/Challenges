plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "com.matthewprenger.cursegradle" version "1.4.0"
}

version = "$project.mod_version"
def config_library_version = "$project.config_library_version"
def arc_version = "$project.arc_version"
def ui_lib_version = "$project.ui_lib_version"

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"
    // Remove the next line if you don't want to depend on the API
    modApi "dev.architectury:architectury-fabric:${rootProject.architectury_version}"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }

    modImplementation "curse.maven:supermartijn642s-config-lib-438332:${project.config_library_file_fabric}"
    modImplementation "curse.maven:arc-883593:${project.arc_file_fabric}"
    modImplementation "curse.maven:ui-933200:${project.ui_lib_file_fabric}"
}

processResources {
    inputs.property "version", project.version
    inputs.property "config_library_version", config_library_version
    inputs.property "arc_version", arc_version
    inputs.property "ui_lib_version", ui_lib_version

    filesMatching("fabric.mod.json") {
        expand([
                "version": project.version,
                "config_library_version": config_library_version,
                "arc_version": arc_version,
                "ui_lib_version": ui_lib_version
        ])
    }
}

shadowJar {
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier.set("fabric-dev-shadow")
}

remapJar {
    injectAccessWidener = true
    inputFile.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier.set("fabric")
}

jar {
    archiveClassifier.set("dev")
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

remapSourcesJar {
    archiveClassifier.set("fabric-sources")
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}

curseforge {
    apiKey = System.getenv('CURSEFORGE_API_KEY')
    project {
        id = rootProject.curse_forge_project_id
        releaseType = rootProject.curse_forge_release_type
        addGameVersion "$rootProject.minecraft_version"
        addGameVersion "Java 17"
        addGameVersion "Fabric"

        relations {
            requiredDependency("architectury-api")
            requiredDependency("supermartijn642s-config-lib")
            requiredDependency("arc")
            requiredDependency("ui")
        }

        mainArtifact(remapJar) {
            displayName = "Challenges Fabric $rootProject.minecraft_version - $rootProject.mod_version"
        }

        addArtifact(remapSourcesJar) {
        }
    }
    options {
        forgeGradleIntegration = false
    }
}

afterEvaluate {
    tasks.curseforge931377.dependsOn remapJar
}